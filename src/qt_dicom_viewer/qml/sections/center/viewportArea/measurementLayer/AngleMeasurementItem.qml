pragma ComponentBehavior: Bound
import QtQuick
import QtQuick.Shapes
import "../../../../theme"

Item {
    id: root
    property var preferences: ({})
    readonly property var styleSettings: preferences.measurement ?? ({})
    readonly property bool dashed: isDraft || isSelected ? (styleSettings.editingDash ?? true) : (styleSettings.completedDash ?? false)
    required property var measurement
    required property var mappedPoints
    required property bool isDraft
    required property bool isSelected
    property alias labelItem: measurementLabel
    readonly property color lineColor: isDraft || isSelected ? (styleSettings.editingColor ?? Theme.measurementSelected) : (styleSettings.completedColor ?? Theme.measurementPrimary)
    readonly property point a: mappedPoints[0] ?? Qt.point(0, 0)
    readonly property point vertex: mappedPoints[1] ?? Qt.point(0, 0)
    readonly property point b: mappedPoints[2] ?? Qt.point(0, 0)
    readonly property real startAngle: Math.atan2(a.y - vertex.y, a.x - vertex.x) * 180 / Math.PI
    readonly property real sweepAngle: {
        const end = Math.atan2(b.y - vertex.y, b.x - vertex.x) * 180 / Math.PI
        return ((end - startAngle + 540) % 360) - 180
    }
    readonly property real arcRadius: Math.min(24,
        Math.hypot(a.x - vertex.x, a.y - vertex.y) / 3,
        Math.hypot(b.x - vertex.x, b.y - vertex.y) / 3)

    Shape {
        anchors.fill: parent
        ShapePath {
            strokeColor: root.lineColor
            strokeWidth: root.styleSettings.lineWidth ?? 1.5
            fillColor: "transparent"
            strokeStyle: root.dashed ? ShapePath.DashLine : ShapePath.SolidLine
            dashPattern: [4, 2]
            startX: root.a.x
            startY: root.a.y
            PathLine { x: root.vertex.x; y: root.vertex.y }
            PathLine { x: root.b.x; y: root.b.y }
        }
        ShapePath {
            strokeColor: root.arcRadius > 1 ? root.lineColor : "transparent"
            strokeWidth: 1
            fillColor: "transparent"
            PathAngleArc {
                centerX: root.vertex.x
                centerY: root.vertex.y
                radiusX: root.arcRadius
                radiusY: root.arcRadius
                startAngle: root.startAngle
                sweepAngle: root.sweepAngle
            }
        }
    }
    Repeater {
        model: root.mappedPoints
        Rectangle {
            required property var modelData
            width: 6; height: 6; radius: 3
            x: modelData.x - 3; y: modelData.y - 3
            visible: root.isDraft || root.isSelected
            color: root.lineColor
        }
    }
    Text {
        id: measurementLabel
        objectName: "measurementLabel"
        x: Math.max(4, Math.min(root.width - width - 4, root.vertex.x + 14))
        y: Math.max(4, Math.min(root.height - height - 4, root.vertex.y + 14))
        text: root.measurement.label ?? ""
        color: root.lineColor
        font.pixelSize: root.styleSettings.fontSize ?? 13
        font.bold: true
        style: Text.Outline
        styleColor: Theme.overlayOutline
    }
}
